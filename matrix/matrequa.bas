10 CLS:INPUT "ENTER NUMBER OF EQUATIONS";N:DIM A(N,2*N),B(N),C(N)
20 FOR I = 1 TO N:FOR J = 1 TO N:PRINT@192,CHR$(31);:PRINT@192,"ENTER COEFFICIENT";I;J;:INPUTA(I,J)
30 NEXT:PRINT @192,CHR$(31);:PRINT@192,"ENTER RIGHT HAND SIDE OF EQUATION";I;:INPUT B(I):NEXT
40 FOR I = 1 TO N:FOR J = 1 TO N:A(I,J+N)=0:NEXT:FOR I = 1 TO N:A(I,N+I)=1:NEXT:GOSUB 80:IF DV=0PRINT "THERE IS NO ONE DEFINED SOLUTION":END
50 PRINT "DETERMINANT VALUE="DV
60 GOSUB 430:FOR I = 1 TO N:PRINT "THE VALUE OF COEFFICIENT"I"IS ";C(I):NEXT
70 END
80 'THIS FINDS THE INVERSE OF A MATRIX USING GAUSSIAN ELIMINATION"
90 FACT=1:M=2
100 FOR I = 1 TO N:IF A(I,I)=0 THEN 140
110 NEXT I:GOTO 160
120 'SWAPS ROWS AND COLUMNS TO PUT NON-ZERO ELEMENTS
130 'ALONG THE DIAGANOL
140 FOR J= 1 TO N:IF J=I THEN NEXT ELSE IF A(I,J)<>0 THEN S=J:D=I:GOSUB 320:GOTO 110ELSE NEXT:FOR J = 1 TO N*2:IFJ=I THEN NEXT ELSE IF A(I,J)<>0 THEN NS=J:D=I:GOSUB 340:GOTO 110 ELSE NEXT
150 'THIS PERFORMS GAUSSIAN ELIMINATION ON THE LOWER TRIANGLE
160 J=1
170 FOR I =NTOMSTEP-1:IFA(I,J)=0THEN 190
180 FOR K = J TO N:IF A(K,J)<>0THEN R=K:MF=-A(I,J)/A(K,J):GOSUB 370ELSENEXTK:DV=0:RETURN
190 NEXT I:M=M+1:J=J+1:IFJ=NTHEN210ELSE 170
200 'THIS PERFORMS GAUSSIAN ELIMINATION ON THE UPPER TRIANGLE
210 M=N-1:J=N
220 FOR I = M TO 1STEP-1:IFA(I,J)=0THEN 230ELSEFORK=JTO1STEP-1:IFA(K,J)<>0THENR=K:MF=-A(I,J)/A(K,J):GOSUB 370ELSE NEXT:DV=0:RETURN
230 NEXTI:M=M-1:J=J-1:IFJ=1THEN280ELSE220
240 'THIS FIRST MULTIPLIES ALONG THE DIAGANOL TO FIND THE
250 'THE VALUE OF THE DETERMINANT OF THE MATRIX, AND THEN DIVIDES
260 'EACH ELEMENT ALONG THE DIAGONAL BY ITSELF TO FIND THE
270 'INVERSE OF THE MATRIX
280 DV=FACT:FOR I = 1 TO N:DV=DV*A(I,I):IF A(I,I)=0 THEN RETURN
290 IF A(I,I)<>1 THEN MF=1/A(I,I):GOSUB 390
300 NEXT:RETURN
310 'THIS SWAPS THE COLUMNS IN S WITH THE COLUMN IN D
320 FOR K = 1 TO N*2:TEMP=A(S,K):A(S,K)=A(D,K):A(D,K)=TEMP:NEXTK:FACT=-FACT:RETURN
330 'THIS SWAPS THE ROW IN S WITH THE ROW IN D
340 FOR K = 1 TO N"TEMP=A(K,S):A(K,S)=A(K,D):A(K,D)=TEMP:NEXTK:FACT=-FACT:RETURN
350 'THIS ADDS TO EACH ELEMENT OR ROW I THE CORRESPONDING
360 'ELEMENT OF ROW R MULTIPLIED BY MF
370 FOR P = 1 TO N*2:A(I,P)=A(I,P)+A(R,P)*MF:NEXT P:RETURN
380 'THIS MULTIPLES EACH ELEMENT OF ROW I BY MF
390 FOR P = 1 TO N*2:A(I,P)=A(I,P)*MF:NEXT P:RETURN
400 'THIS LEFT MULTIPLIES THE INVERSE OF THE MATRIX BY THE
410 'RIGHT HAD SIDE MATRIX TO SOLVE THE EQUATIONS, LEAVING
420 'THE VALUES OF THE COEFFIENTS
430 FOR I = 1 TO N:C(I)=0:FOR K = 1 TO N:C(I)=C(I)+A(I,K+N)*B(K):NEXT K,I:RETURN

